"""
    integrate(func::Function, f::AbstractFilament, i::Int, quad::AbstractQuadrature)
    integrate(func::Function, s::Segment, quad::AbstractQuadrature)

Estimate integral along a filament segment using the chosen quadrature.

Integration is performed in the segment `(f[i], f[i + 1])`.

The function `func(Î¶)` takes a single argument ``Î¶ âˆˆ [0, 1]`` which corresponds
to the position of a point within the segment.

# Examples

Estimate arc length of segment ``[i, i + 1]``, given by ``â„“ = âˆ«_{t_{i}}^{t_{i + 1}} |âˆ‚_t \\bm{X}(t)| \\, \\mathrm{d}t``:

```julia
quad = GaussLegendre(4)  # quadrature rule
â„“ = integrate(f, i, quad) do Î¶
    norm(f(i, Î¶, Derivative(1)))  # = |âˆ‚â‚œğ—|
end
```
"""
function integrate(
        func::F, f::AbstractFilament, i::Int, quad::AbstractQuadrature;
        _args = _prepended_args(func, f, i),  # used internally
    ) where {F}
    Î¶s, ws = quadrature(quad)
    ts = knots(f)
    Î”t = ts[i + 1] - ts[i]
    Î”t * sum(eachindex(ws)) do j
        @inline
        fx = @inline func(_args..., Î¶s[j])
        ws[j] * fx
    end
end

# Depending on the number of arguments required by the function, prepend some arguments.
function _prepended_args(func::F, f::AbstractFilament, i) where {F}
    if hasmethod(func, Tuple{Any,Any,Any})  # func(f, i, Î¶)
        (f, i)
    elseif hasmethod(func, Tuple{Any})      # func(Î¶)
        ()
    end
end

function _prepended_args(func::F, s::Segment) where {F}
    if hasmethod(func, Tuple{Any,Any})  # func(seg, Î¶)
        (s,)
    else
        _prepended_args(func, s.f, s.i)
    end
end

function integrate(func::F, s::Segment, quad::AbstractQuadrature) where {F}
    (; f, i,) = s
    integrate(func, f, i, quad; _args = _prepended_args(func, s))
end

"""
    integrate(func::Function, f::AbstractFilament, quad::AbstractQuadrature)

Estimate integral over a whole filament.

The integral is computed as the sum of the integrals over each filament segment.
The given quadrature rule is applied over each segment, meaning that the total number of
evaluations is given by the number of segments multiplied by the length of the quadrature
rule.

The integrated function can take either 2 or 3 arguments, in which case they are
respectively interpreted as `func(seg::Segment, Î¶::Real)` or
`func(f::AbstractFilament, i::Int, Î¶::Real)`.

## Examples

Estimate the total length of a closed filament, ``L = âˆ® |âˆ‚_t \\bm{X}(t)| \\, \\mathrm{d}t``:

```julia
quad = GaussLegendre(4)  # quadrature rule

# Alternative 1
L = integrate(f, quad) do seg, Î¶
    norm(seg(Î¶, Derivative(1)))  # = |âˆ‚â‚œğ—|
end

# Alternative 2
L = integrate(f, quad) do ff, ii, Î¶
    norm(ff(ii, Î¶, Derivative(1)))  # = |âˆ‚â‚œğ—|
end
```
"""
function integrate(func::F, f::AbstractFilament, quad::AbstractQuadrature) where {F}
    sum(segments(f)) do seg
        integrate(func, seg, quad)
    end
end

using Base: @propagate_inbounds

@doc raw"""
    integrate(integrand::Function, f::AbstractFilament, i::Int, quad::AbstractQuadrature)
    integrate(integrand::Function, s::Segment, quad::AbstractQuadrature)

Estimate integral along a filament segment using the chosen quadrature.

Integration is performed in the segment `(f[i], f[i + 1])`.

The function `integrand(Î¶)` takes a single argument ``Î¶ âˆˆ [0, 1]`` which corresponds
to the position of a point within the segment.

# Examples

Estimate arc length of segment ``[i, i + 1]``, given by ``â„“ = âˆ«_{t_{i}}^{t_{i + 1}} |âˆ‚_t \bm{X}(t)| \, \mathrm{d}t``:

```julia
quad = GaussLegendre(4)  # quadrature rule
â„“ = integrate(f, i, quad) do f, i, Î¶
    norm(f(i, Î¶, Derivative(1)))  # = |âˆ‚â‚œğ—|
end
```

Alternatively:
```julia
quad = GaussLegendre(4)  # quadrature rule
s = Segment(f, i)
â„“ = integrate(s, quad) do s, Î¶
    norm(s(Î¶, Derivative(1)))  # = |âˆ‚â‚œğ—|
end
```
"""
function integrate(
        integrand::F, f::AbstractFilament, i::Int, quad::AbstractQuadrature;
        _args = (f, i),  # integrand arguments; used internally
    ) where {F}
    Î¶s, ws = quadrature(quad)
    ts = knots(f)
    Î”t = ts[i + 1] - ts[i]
    Î”t * sum(eachindex(ws)) do j
        @inline
        fx = @inline integrand(_args..., Î¶s[j])
        ws[j] * fx
    end
end

function integrate(integrand::F, s::Segment, quad::AbstractQuadrature) where {F}
    integrate(integrand, s.f, s.i, quad; _args = (s,))
end

"""
    integrate(integrand::Function, f::AbstractFilament, quad::AbstractQuadrature)

Estimate integral over a whole filament.

The integral is computed as the sum of the integrals over each filament segment.
The given quadrature rule is applied over each segment, meaning that the total number of
evaluations is given by the number of segments multiplied by the length of the quadrature
rule.

The signature of the integrated function must be
`integrand(f::AbstractFilament, i::Int, Î¶::Real)`, where `i` is the index of the segment of
interest. See below for some examples.

## Examples

Estimate the total length of a closed filament, ``L = âˆ® |âˆ‚_t \\bm{X}(t)| \\, \\mathrm{d}t``:

```julia
quad = GaussLegendre(4)  # quadrature rule

integrand(f, i, Î¶) = f(i, Î¶, Derivative(1))  # = |âˆ‚â‚œğ—|

# Here `f` is an existent filament.
L = integrate(integrand, f, quad)

# Or, more conveniently, using a `do` block to define an anonymous function.
L = integrate(f, quad) do ff, i, Î¶
    norm(ff(i, Î¶, Derivative(1)))  # = |âˆ‚â‚œğ—|
end
```
"""
function integrate(integrand::F, f::AbstractFilament, quad::AbstractQuadrature) where {F}
    sum(segments(f)) do seg
        integrate(integrand, seg.f, seg.i, quad)
    end
end

## ====================================================================================== ##
#  No quadrature case: the idea is to only evaluate quantities on filament nodes,
#  avoiding any interpolation.
## ====================================================================================== ##

# This is an internal type which is just used to replace the interpolation syntax
# f(i, Î¶, args...) by the on-node evaluation syntax f[i, args...].
# When integrating, we consider that we evaluate in the middle of each segment (at Î¶ = 1/2).
# Moreover, the location and the derivative at that point are approximated from the
# locations at the two extremities of the segment.
struct NoQuadFilament{F <: AbstractFilament}
    f :: F
end

@propagate_inbounds (u::NoQuadFilament)(i::Int, Î¶::Number) =
    u(i, Î¶, Derivative(0))

@propagate_inbounds (u::NoQuadFilament)(i::Int, Î¶::Number, ::Derivative{0}) =
    (u.f[i] + u.f[i + 1]) ./ 2

@propagate_inbounds function (u::NoQuadFilament)(i::Int, Î¶::Number, ::Derivative{1})
    ts = knots(u.f)
    (u.f[i + 1] - u.f[i]) ./ (ts[i + 1] - ts[i])
end

@propagate_inbounds (u::NoQuadFilament)(i, Î¶::Number, d) = u.f[i, d]  # not sure if this is used...

function integrate(
        integrand::F, f::AbstractFilament, i::Int, quad::NoQuadrature;
        _args = (),
    ) where {F}
    ts = knots(f)
    args = _noquad_replace_args(_args...)  # replaces any AbstractFilament by a NoQuadFilament
    Î”t = ts[i + 1] - ts[i]
    Î¶ = 0.5  # unused
    Î”t * integrand(args..., Î¶)
end

_noquad_replace_args(f::AbstractFilament, args...) = (NoQuadFilament(f), _noquad_replace_args(args...)...)
_noquad_replace_args(x::Any, args...) = (x, _noquad_replace_args(args...)...)
_noquad_replace_args() = ()
